10 CLS:FOR I=0TO143:X=I%12:Y=I/12
20 IF X=0 OR X=11 OR Y=0 OR Y=11 OR I%19=0 OR I%26=0 LC X,Y:?CHR$(1)
30 NEXT:LC0,15:?"ﾔﾘﾅｵｼ(R)":LC0,16:?"ﾂｷﾞﾉﾊﾟｽﾞﾙ(N)":GOTO50
40 LC G,H:?CHR$(0):FOR I=0TO2:LC[I*2],[I*2+1]:?CHR$(0):NEXT
50 M=65:LC0,13:?"ｳｺﾞｸ(A-C):"
60 G=RND(10)+1:H=RND(10)+1:LC G,H:IF SCR()=0 ?"a" ELSE GOTO60
70 FOR I=0TO2
80 [I*2]=RND(10)+1:[I*2+1]=RND(10)+1:LC[I*2],[I*2+1]:IF SCR()<>0 GOTO80
90 IF[0]=G OR[1]=H GOTO80
100 ?CHR$(65+I):NEXT:FOR I=0TO5:[I+6]=[I]:NEXT
110 LC10,13:?CHR$(M):R=(M-65)*2:P=0:IF[R]=G AND[R+1]=H P=97
120 K=INKEY():IF 65<=K AND K<=67 M=K:GOTO110
130 IF K=82 GOSUB180
140 IF K=78 GOTO40
150 X=(K=RIGHT)-(K=LEFT):Y=(K=DOWN)-(K=UP):Q=SCR([R]+X,[R+1]+Y):IF Q=0 OR Q=97 LC[R],[R+1]:?CHR$(P):[R]=[R]+X:[R+1]=[R+1]+Y:LC[R],[R+1]:?CHR$(M):P=Q:WAIT5:GOTO150
160 IF[0]=G AND[1]=H LC0,13:?"ｵﾒﾃﾞﾄｳ!    ":WAIT120:GOTO40
170 GOTO120
180 FOR I=0TO2:LC[I*2],[I*2+1]:?CHR$(0):LC[I*2+6],[I*2+7]:?CHR$(65+I):NEXT
190 LC G,H:?"a":FOR I=0TO5:[I]=[I+6]:NEXT:RETURN
